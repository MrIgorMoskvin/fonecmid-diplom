#Область ОбработчикиСобытийФормы
&НаКлиенте
Процедура ВКМ_ПриОткрытии(Отказ)
	ВКМ_ПоказатьПоляДляАбонентскогоОбслуживания(Объект.ВидДоговора)
КонецПроцедуры

&НаСервере
Процедура ВКМ_ПередЗаписьюНаСервере(Отказ, ТекущийОбъект, ПараметрыЗаписи)
	Если ТекущийОбъект.ВидДоговора = Перечисления.ВидыДоговоровКонтрагентов.ВКМ_АбонентскоеОбслуживание Тогда
		Если Не ЗначениеЗаполнено(ТекущийОбъект.ВКМ_ДатаНачала) Тогда
			Отказ = Истина;
			ОбщегоНазначения.СообщитьПользователю("Заполните дату начала договора");
		КонецЕсли;
		Если Не ЗначениеЗаполнено(ТекущийОбъект.ВКМ_ДатаОкончания) Тогда
			Отказ = Истина;
			ОбщегоНазначения.СообщитьПользователю("Заполните дату окончания договора");
		КонецЕсли;		
	Иначе
		ТекущийОбъект.ВКМ_ДатаНачала = Дата(1,1,1);	
		ТекущийОбъект.ВКМ_ДатаОкончания = Дата(1,1,1);	
		ТекущийОбъект.ВКМ_АбонентскаяПлата = 0;
		ТекущийОбъект.ВКМ_Нормочас = 0;
	КонецЕсли
КонецПроцедуры

&НаСервере
Процедура ВКМ_ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)

	// СтандартныеПодсистемы.ПодключаемыеКоманды
	ПодключаемыеКоманды.ПриСозданииНаСервере(ЭтотОбъект);
	// Конец СтандартныеПодсистемы.ПодключаемыеКоманды

КонецПроцедуры
#КонецОбласти

#Область ОбработчикиСобытийЭлементовШапкиФормы
&НаКлиенте
Процедура ВКМ_ВидДоговораПриИзменении(Элемент)
	ВКМ_ПоказатьПоляДляАбонентскогоОбслуживания(Объект.ВидДоговора)
КонецПроцедуры
#КонецОбласти

#Область СлужебныеПроцедурыИФункции
&НаКлиенте
Процедура ВКМ_ПоказатьПоляДляАбонентскогоОбслуживания(ВидДоговора)
	
	ВидимостьПоляАбонОбсл = (ВидДоговора = ПредопределенноеЗначение("Перечисление.ВидыДоговоровКонтрагентов.ВКМ_АбонентскоеОбслуживание"));
	Для каждого Элемент из Элементы Цикл
		Если ТипЗнч(Элемент) = Тип("ПолеФормы" ) И Лев(Элемент.Имя, 4) = "ВКМ_" Тогда
			Элемент.Видимость = ВидимостьПоляАбонОбсл;
		КонецЕсли
	КонецЦикла
	
КонецПроцедуры

#КонецОбласти