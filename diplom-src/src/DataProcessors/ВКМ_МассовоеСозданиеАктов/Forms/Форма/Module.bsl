#Область ОбработчикиСобытийФормы
&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	ДатаДокумента = ТекущаяДатаСеанса();
	ДатаКонПериода = НачалоМесяца(ДатаДокумента) - 1;
	ДатаНачПериода = НачалоМесяца(ДатаКонПериода);
	
	Объект.Период.ДатаНачала = ДатаНачПериода;
	Объект.Период.ДатаОкончания = ДатаКонПериода;
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиСобытийЭлементовТаблицыФормы

&НаКлиенте
Процедура СписокРеализацийВыбор(Элемент, ВыбраннаяСтрока, Поле, СтандартнаяОбработка)
	Если Поле.Имя = "СписокРеализацийДоговор" Тогда
		СтандартнаяОбработка = Ложь;
		ОткрытьКарточкуДоговора(Элемент.ТекущиеДанные.Договор);	 
	ИначеЕсли Поле.Имя = "СписокРеализацийРеализация" Тогда
    	СтандартнаяОбработка = Ложь;
    	ОткрытьКарточкуРеализации(Элемент.ТекущиеДанные.Реализация);
   КонецЕсли;
КонецПроцедуры 
 
#КонецОбласти


#Область ОбработчикиКомандФормы

&НаКлиенте
Процедура СозданиеАктов(Команда)
	Если ПроверитьЗаполнение() Тогда
		ОповещениеОЗакрытии = Новый ОписаниеОповещения("СозданиеАктовПодтверждение", ЭтотОбъект);
		ТекстВопроса = "Запустить процедуру создания актов об оказанных услугах?";	
		СтандартныеПодсистемыКлиент.ПоказатьВопросПользователю(ОповещениеОЗакрытии, ТекстВопроса, РежимДиалогаВопрос.ДаНет, );
	КонецЕсли;
КонецПроцедуры


&НаКлиенте
Процедура Договор(Команда)
	ОткрытьКарточкуДоговора(Элементы.СписокРеализаций.ТекущиеДанные.Договор);	
КонецПроцедуры

&НаКлиенте
Процедура Реализация(Команда)
	ОткрытьКарточкуРеализации(Элементы.СписокРеализаций.ТекущиеДанные.Реализация);
КонецПроцедуры

#КонецОбласти 

#Область СлужебныеПроцедурыИФункции 
&НаКлиенте
Процедура СозданиеАктовПодтверждение(РезультатВопроса, Значение) Экспорт
	
	Если РезультатВопроса.Значение <> КодВозвратаДиалога.Да Тогда
		Возврат;
	КонецЕсли; 
	
	//Выполнить тяжелую операцию на сервере
	ДлительнаяОперация = СозданиеАктовНаСервере(УникальныйИдентификатор);
	
	ПараметрыОжидания = ДлительныеОперацииКлиент.ПараметрыОжидания(ЭтотОбъект);          
	ПараметрыОжидания.ВыводитьОкноОжидания = Истина;
	ПараметрыОжидания.ВыводитьПрогрессВыполнения = Истина;
	ПараметрыОжидания.Интервал = 1;
	
	ФункцияВыполнена = Новый ОписаниеОповещения("СозданиеАктовНаСервереВыполнено", ЭтотОбъект);
	ДлительныеОперацииКлиент.ОжидатьЗавершение(ДлительнаяОперация, ФункцияВыполнена, ПараметрыОжидания);

КонецПроцедуры

&НаКлиенте
Процедура СозданиеАктовНаСервереВыполнено(Результат, ДополнительныеПараметры) Экспорт

	Если Результат = Неопределено Тогда
		Возврат;
	КонецЕсли;
	
	Если Результат.Статус = "Ошибка" Тогда
		ОбщегоНазначенияКлиент.СообщитьПользователю(Результат.КраткоеПредставлениеОшибки);
		Возврат;
	КонецЕсли;
	
	ЗначениеФункции = ПолучитьИзВременногоХранилища(Результат.АдресРезультата);
    
    Для каждого ТекЗначение Из ЗначениеФункции Цикл    	
    	СтрокаСписка = Объект.СписокРеализаций.Добавить();
    	Если ЗначениеЗаполнено(ТекЗначение.Договор) Тогда
    		СтрокаСписка.Договор = ТекЗначение.Договор;
    	КонецЕсли;
    	Если ЗначениеЗаполнено(ТекЗначение.Реализация) Тогда
    		СтрокаСписка.Реализация = ТекЗначение.Реализация;
    	КонецЕсли;    	
		СтрокаСписка.ДатаСоздания = ТекЗначение.Дата;    	
    КонецЦикла;
    
    ОбщегоНазначенияКлиент.СообщитьПользователю(СтрШаблон("Операция создания актов завершена, создано актов: %1", ЗначениеФункции.Количество()));    
	
КонецПроцедуры

	
&НаСервере
Функция СозданиеАктовНаСервере(УникальныйИдентификатор)
	
	ПараметрыВыполнения = ДлительныеОперации.ПараметрыВыполненияФункции(УникальныйИдентификатор);
	ПараметрыВыполнения.НаименованиеФоновогоЗадания = "Запуск создания актов";
	ПараметрыВыполнения.ЗапуститьВФоне = Истина;
	ДлительнаяОперация = ДлительныеОперации.ВыполнитьФункцию(ПараметрыВыполнения, "Обработки.ВКМ_МассовоеСозданиеАктов.СозданиеАктовВФоне",
	                                                         Объект.Период.ДатаНачала, Объект.Период.ДатаОкончания);   
	
	Возврат ДлительнаяОперация;
	
КонецФункции

&НаКлиенте
Процедура ОткрытьКарточкуДоговора(Договор)
	
	Если ЗначениеЗаполнено(Договор) Тогда
		КарточкаДоговора = ОткрытьФорму("Справочник.ДоговорыКонтрагентов.Форма.ФормаЭлемента", Новый Структура("Ключ", Договор));
		КарточкаДоговора.ТолькоПросмотр = Истина;
	КонецЕсли;
			
КонецПроцедуры

&НаКлиенте
Процедура ОткрытьКарточкуРеализации(Реализация)

	Если ЗначениеЗаполнено(Реализация) Тогда
		КарточкаРеализации = ОткрытьФорму("Документ.РеализацияТоваровУслуг.Форма.ФормаДокумента", Новый Структура("Ключ", Реализация));
		КарточкаРеализации.ТолькоПросмотр = Истина;
	КонецЕсли;

КонецПроцедуры

#КонецОбласти

